plugins {
    id "java"
    id "maven"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.1"
}

// This Gradle plugin is published under the group "gradle.plugin.org.embulk".
// We have an option to avoid the "gradle.plugin" prefix, but we don't do it as it bothers the Gradle team.
// They want Gradle plugins to be published under the "gradle.plugin" prefix for some security reasons.
// https://plugins.gradle.org/docs/publish-plugin
group = "org.embulk"
version = "0.2.2"
description = "A Gradle plugin to build and publish Embulk plugins"

repositories {
    mavenCentral()
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation gradleApi()

    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.0"
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

jar {
    from rootProject.file("LICENSE")
}

gradlePlugin {
    plugins {
        embulkPluginsPlugin {
            id = "org.embulk.embulk-plugins"
            displayName = "A Gradle plugin for Embulk plugins"
            description = "${project.description}"
            implementationClass = "org.embulk.gradle.embulk_plugins.EmbulkPluginsPlugin"
        }
    }
}

pluginBundle {
    website = "http://www.embulk.org/"
    vcsUrl = "https://github.com/embulk/gradle-embulk-plugins"
    tags = ["embulk"]
}
